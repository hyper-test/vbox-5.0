static String getSysProp(String value, String fallback) {
    // This is required as there is a length limitation on System.properties.getProperty(a,b)

    if (!System.properties.containsKey(value)) {
        return fallback
    }

    return System.properties.get(value)
}

import java.nio.file.Paths

def modType = 'xpcom'
def modId = "vbox-${gradle.vboxVersion}-${modType}"

apply plugin: 'java'

dependencies {
    compileOnly 'io.kamax:hbox-server-api:0.0.13-2-gcf752f2'
    compileOnly files("lib/vboxj${modType}.jar")
    compile project(":hbox-vbox-${gradle.vboxVersion}-server-core")
}

def binPath = Paths.get(
        getSysProp("server.build.bin.linux.modules",
                getSysProp("server.build.bin.modules",
                        project.buildDir.toPath().resolve('bin').toString()
                )
        )
)
def jarPath = binPath.resolve(modId)

task distBinJar(type: Copy) {
    from jar
    from project.file("lib/vboxj${modType}.jar")
    from configurations.compile
    into jarPath
}

task distBinMeta(type: Copy) {
    from project.file("src/package/${modId}.xml")
    into binPath
}

task distBinLinux {
    dependsOn distBinJar, distBinMeta
}
